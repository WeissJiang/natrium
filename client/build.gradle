plugins {
    id 'com.github.node-gradle.node'
    id 'java-library'
}

group = 'nano'

sourceCompatibility = '16'
targetCompatibility = '16'

task installNodeModules(type: NpmTask) {
    args = ['install']
}

task checkNodeModules {
    if (!file('node_modules').exists()) {
        dependsOn installNodeModules
    }
}

task start(type: NpmTask) {
    dependsOn checkNodeModules
    args = ['start']
}

task buildStatic(type: NpmTask) {
    dependsOn checkNodeModules
    args = ['run', 'build']
}

task cleanStatic(type: NpmTask) {
    dependsOn checkNodeModules
    args = ['run', 'clean']
}

clean {
    dependsOn cleanStatic
}

processResources {
    if (!file('dist').exists() || gradle.startParameter.taskNames.contains('clean')) {
        dependsOn buildStatic
    }

    from('dist') {
        into '/META-INF/resources'
    }
}

node {
    version = '15.14.0'
    npmVersion = '7.10.0'
    download = true
}

