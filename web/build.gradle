plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

group = 'nano'

sourceCompatibility = '14'
targetCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.compilerArgs += '--enable-preview'
}

tasks.withType(JavaExec) {
    jvmArgs += '--enable-preview'
}

tasks.withType(Test) {
    jvmArgs += '--enable-preview'
}

def staticModulesDir = "${buildDir}/static-modules"
task staticModules {

    doLast {
        def destDir = mkdir(staticModulesDir)
        ant.get(src: 'https://cdnjs.cloudflare.com/ajax/libs/less.js/3.12.2/less.js', dest: destDir, skipexisting: true)
        ant.get(src: 'https://cdnjs.cloudflare.com/ajax/libs/typescript/4.0.2/typescript.js', dest: destDir, skipexisting: true)
    }
}

processResources {
    dependsOn staticModules
    from(staticModulesDir) {
        into 'static/modules'
    }
}

dependencies {
    // web jars
    implementation('org.webjars.npm:react:16.13.1') {
        exclude group: 'org.webjars.npm'
    }
    implementation('org.webjars.npm:react-dom:16.13.1') {
        exclude group: 'org.webjars.npm'
    }
    implementation('org.webjars.npm:less:3.11.3') {
        exclude group: 'org.webjars.npm'
    }
    implementation 'org.webjars.npm:typescript:3.9.5'
    // json path
    implementation 'com.jayway.jsonpath:json-path:2.4.0'
    // javascript
    implementation 'org.graalvm.js:js:20.0.0'
    implementation 'org.graalvm.js:js-scriptengine:20.0.0'
    // postgresql
    implementation 'org.postgresql:postgresql:42.2.16'
    // spring
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    // project common
    implementation project(':common')
    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // test
    testImplementation 'org.springframework.amqp:spring-rabbit-test'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
    // test environment
    environment([
            // nano
            '--nano.nanoApi'              : 'http://localhost:8000',
            '--nano.nanoToken'            : 'token',
            // postgres
            '--spring.datasource.url'     : 'jdbc:postgresql://localhost:5432/postgres',
            '--spring.datasource.username': 'postgres',
            '--spring.datasource.password': 'password'
    ])
}
